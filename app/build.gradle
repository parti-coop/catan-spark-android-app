apply plugin: 'com.android.application'
check.dependsOn 'assembleDebugAndroidTest'

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "xyz.parti.catan"
        targetSdkVersion 33
        minSdkVersion 28
        versionCode 29
        versionName "1.0.16"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    signingConfigs {
        release {
            def store_password = properties.getProperty('signing.config.store.password')
            def key_alias = properties.getProperty('signing.config.key.alias')
            def key_password = properties.getProperty('signing.config.key.password')

            storeFile file("parti.keystore")
            storePassword store_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    buildTypes {
        def var_api_base_url = "API_BASE_URL"
        def var_is_debug = "IS_DEBUG"
        def var_api_base_url_regx = "API_BASE_URL_REGX"
        def debug_api_base_url = properties.getProperty('debug.api.base.url')
        def debug_api_base_url_regx = properties.getProperty('debug.api.base.url.regx')

        def var_auth_google_webclient_id = "AUTH_GOOGLE_WEBCLIENT_ID"
        def debug_auth_google_webclient_id = properties.getProperty('debug.auth.google.webclient.id')
        def release_auth_google_webclient_id = properties.getProperty('release.auth.google.webclient.id')

        def var_auth_facebook_app_id = "auth_facebook_app_id"
        def debug_auth_facebook_app_id = properties.getProperty('debug.auth.facebook.app.id')
        def release_auth_facebook_app_id = properties.getProperty('release.auth.facebook.app.id')

        def var_auth_facebook_client_token = "auth_facebook_client_token"
        def debug_auth_facebook_client_token = properties.getProperty('debug.auth.facebook.client.token')
        def release_auth_facebook_client_token = properties.getProperty('release.auth.facebook.client.token')

        def var_auth_fb_login_protocol_scheme = "auth_fb_login_protocol_scheme"
        def debug_auth_fb_login_protocol_scheme = properties.getProperty('debug.auth.fb.login.protocol.scheme')
        def release_auth_fb_login_protocol_scheme = properties.getProperty('release.auth.fb.login.protocol.scheme')

        debug {
            buildConfigField "boolean", var_is_debug, "true"
            buildConfigField "String", var_api_base_url, ( '"' + debug_api_base_url + '"' ?: "\"http://parti.test/\"" )
            buildConfigField "String", var_api_base_url_regx, ( '"' + debug_api_base_url_regx + '"' ?: "\"^https:\\\\/\\\\/(.*\\\\.)?parti\\\\.test(\$|\\\\/)\"" )
            buildConfigField "String", var_auth_google_webclient_id, '"' + debug_auth_google_webclient_id + '"'
            resValue "string", var_auth_facebook_app_id, '"' + debug_auth_facebook_app_id + '"'
            resValue "string", var_auth_facebook_client_token, '"' + debug_auth_facebook_client_token + '"'
            resValue "string", var_auth_fb_login_protocol_scheme, '"' + debug_auth_fb_login_protocol_scheme + '"'
            // manifestPlaceholders = [ crashlyticsApiKey: '767a814309d5e5b2803f20b7e992ad890513b53d' ]
        }
        release {
            buildConfigField "boolean", var_is_debug, "false"
            buildConfigField "String", var_api_base_url, "\"https://parti.xyz/\""
            buildConfigField "String", var_api_base_url_regx, "\"^https:\\\\/\\\\/(.*\\\\.)?parti\\\\.xyz(\$|\\\\/)\""
            buildConfigField "String", var_auth_google_webclient_id, '"' + release_auth_google_webclient_id + '"'
            resValue "string", var_auth_facebook_app_id, '"' + release_auth_facebook_app_id + '"'
            resValue "string", var_auth_facebook_client_token, '"' + release_auth_facebook_client_token + '"'
            resValue "string", var_auth_fb_login_protocol_scheme, '"' + release_auth_fb_login_protocol_scheme + '"'
            // manifestPlaceholders = [ crashlyticsApiKey: '61fec037830f072a57601a3e9f538a4a0f0d1ac3' ]
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def newName = output.outputFile.name.replace("app", "parti");
            newName = newName.replace(".apk", "-${variant.versionName}.apk")
            outputFileName = "./" + newName
        }
    }

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildToolsVersion '30.0.3'
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:27.1.0'
}

repositories {
    maven { url 'https://maven.google.com' }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.karumi:dexter:4.2.0'

    implementation 'com.google.firebase:firebase-core:16.0.0'
    implementation 'com.google.firebase:firebase-messaging:17.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.7'
    implementation 'com.google.android.gms:play-services-auth:20.7.0'
    implementation 'com.firebase:firebase-jobdispatcher:0.6.0'
    implementation 'com.facebook.android:facebook-android-sdk:16.2.0'

    implementation 'com.wang.avi:library:2.1.3'


    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
